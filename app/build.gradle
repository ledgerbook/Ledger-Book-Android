apply plugin: 'com.android.application'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.android.ledgerbook"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("etc/debug.keystore")
        }
        release {
            if (rootProject.file('release.properties').exists()) {
                def props = new Properties()
                props.load(rootProject.file('release.properties').newDataInputStream())
                release {
                    storeFile rootProject.file(props.storeFile)
                    storePassword props.storePassword
                    keyAlias props.keyAlias
                    keyPassword props.keyPassword
                }
            }
        }
    }
    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL", '"http://core.paysense.io"'
            resValue 'string', 'APP_NAME', '"LedgerBook-Debug"'
            applicationIdSuffix '.debug'
        }
        release {
            buildConfigField "String", "BASE_URL", '"https://api.gopaysense.com"'
            resValue 'string', 'APP_NAME', '"Ledger-Book"'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (rootProject.file('release.properties').exists()) {
                signingConfig signingConfigs.release
            }
        }
        debugRelease {
            initWith debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue 'string', 'APP_NAME', '"LedgerBook-Debug-Release"'
        }
    }

    sourceSets {
        debugRelease {
            setRoot('src/debug')
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${version}.apk"
        }
        if (variant.getBuildType().isMinifyEnabled()) {
            variant.assemble.doLast {
                copy {
                    from variant.mappingFile
                    into variant.mappingFile.parent
                    rename { String fileName ->
                        "mapping-${variant.name}-${version}.txt"
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'

    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraintLayoutVersion"
    implementation "com.jakewharton:butterknife:$rootProject.butterknifeVersion"
    implementation "com.squareup.picasso:picasso:$rootProject.picassoVersion"
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.retrofitLoggingInterceptorVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "org.greenrobot:eventbus:$rootProject.eventBusVersion"
    implementation "com.github.franmontiel:PersistentCookieJar:$rootProject.persistentCookieJarVersion"
    implementation('com.crashlytics.sdk.android:answers:1.4.1') { transitive = true; }
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') { transitive = true; }
    implementation "com.jakewharton.picasso:picasso2-okhttp3-downloader:$rootProject.okhttp3DownloaderVersion"

    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknifeVersion"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
